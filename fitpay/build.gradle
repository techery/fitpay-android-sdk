apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven'
apply from: '../jacoco.gradle'

group = 'com.fitpay.android'
version = '0.4.26'
archivesBaseName = "android_sdk"

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'file://' + new File(System.getProperty('user.home'), 'LocalRepository').absolutePath) {
            }

            snapshotRepository(url: 'file://' + new File(System.getProperty('user.home'), 'LocalRepository/snapshots').absolutePath) {
            }

            pom.project {
                name 'android_sdk'
                packaging 'aar'
                description 'Android SDK'
                url 'http://private-git-url'

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/fitpay/fitpay-android-sdk/blob/develop/LICENSE'
                    }
                }

                developers {
                    developer {
                        id '123'
                        name 'yourName'
                        email 'your@email.com'
                    }
                }
            }
        }
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 25
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }

    buildTypes.each {
        it.buildConfigField "String", 'SDK_VERSION', "\"" + version + "\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
        }
        unitTests.returnDefaultValues = true
    }
}

ext {
    supportLib = "25.3.1"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:support-annotations:${supportLib}"
    compile "com.android.support:appcompat-v7:${supportLib}"

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.0'

    compile 'io.reactivex:rxjava:1.3.0'
    compile 'io.reactivex:rxandroid:1.2.1'

    compile 'me.alexrs:prefs:1.1.0'

    compile 'org.bouncycastle:bcprov-jdk15on:1.54'
    compile 'com.nimbusds:nimbus-jose-jwt:4.22'

    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'junit:junit:4.12'

    testCompile('org.robolectric:robolectric:3.0') {
        exclude module: 'junit'
        exclude group: 'org.bouncycastle'
    }
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}
generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('bintray.properties').newDataInputStream())
    user = properties.getProperty("bintray_user")
    key = properties.getProperty("bintray_key")

    pkg {
        repo = 'maven'
        name = 'com.fitpay'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/fitpay/fitpay-android-sdk.git'

//        version {
//            name = '0.4.13'
//        }
    }
    configurations = ['archives']
}

// Test Logging
tasks.withType(Test) {
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}

